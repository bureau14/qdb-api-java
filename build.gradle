apply plugin: 'java'
group = 'net.quasardb'
archivesBaseName = 'qdb'

String arch = System.getProperty("os.arch").toLowerCase();
if (arch.endsWith("64")) arch = "x64"
else arch = "x86"

String os = System.getProperty("os.name").toLowerCase();
if (os.startsWith("windows")) os = "windows"
if (os.startsWith("mac")) os = "macosx"

repositories {
   mavenCentral()
   flatDir {
       dirs 'qdb'
   }
}

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.5"
  }
}

apply plugin: "me.champeau.gradle.jmh"

dependencies {
   compile group: 'junit', name: 'junit', version: '4.+'
   compile group: 'net.quasardb', name: 'jni'
   runtime group: 'net.quasardb', name: 'jni', classifier: "$os-$arch"
}

javadoc {
    options.noQualifiers = ['all']
    options.links = ['https://docs.oracle.com/javase/7/docs/api/']
}

test {
    systemProperty "qdbd.path", project.getProperty("qdbd.path")
}

jmh {
  includeTests = true
  fork = 1
  warmupIterations = 3
  iterations = 10
  exclude = ['Timespec']
}

task flatSources(type: Copy) {
    from sourceSets.main.allSource.files
    into "$buildDir/flatSrc/net/quasardb/qdb"
}

task sourcesJar(type: Jar, dependsOn: flatSources) {
    classifier = 'sources'
    from "$buildDir/flatSrc"
}

task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives testsJar
    archives javadocJar
}
