apply plugin: 'java'
group = 'net.quasardb.qdb'
version = '2.0.0'
archivesBaseName = 'net.quasardb.qdb'

String arch = System.getProperty("os.arch").toLowerCase();
if (arch.endsWith("64")) arch = "x64"
else arch = "x86"

String os = System.getProperty("os.name").toLowerCase();
if (os.startsWith("windows")) os = "windows"
if (os.startsWith("mac")) os = "macosx"

repositories {
   mavenCentral()
   flatDir {
       dirs 'qdb'
   }
}

dependencies {
   compile group: 'junit', name: 'junit', version: '4.+'
   compile name: "net.quasardb.qdb.jni", version: "$version"
   runtime name: "net.quasardb.qdb.jni", version: "$version", classifier: "$os-$arch"
}

javadoc {
    options.noQualifiers = ['all']
    options.links = ['https://docs.oracle.com/javase/7/docs/api/']
}

test {
    systemProperty "qdbd.path", project.getProperty("qdbd.path")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives testsJar
    archives javadocJar
}
