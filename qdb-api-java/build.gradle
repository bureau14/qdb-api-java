apply plugin: 'java'
group = 'net.quasardb.qdb'
version = '2.0.0'

jar {
    doFirst {
        String osName = System.getProperty("os.name")
        if (osName.startsWith("Windows"))
            appendix = "win" + System.getProperty("sun.arch.data.model")
        else if (osName == "Mac OS X")
            appendix = "darwin"
        else if (osName == "FreeBSD")
            appendix = "freebsd"
        else if (osName == "Linux")
            appendix = "linux"
    }
    from {[
        // Windows
        "../qdb/bin/qdb_api.dll",
        "../qdb/bin/qdb_api_jni.dll",

        // OS X
        "../qdb/lib/libqdb_api.dylib",
        "../qdb/lib/libqdb_api_jni.jnilib",

        // FreeBSD and Linux
        "../qdb/lib/libqdb_api.so",
        "../qdb/lib/libqdb_api_jni.so"
    ]}
}

sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir '../qdb/jni'
        }
    }
}

// http://docs.oracle.com/javase/1.5.0/docs/tooldocs/windows/javadoc.html#options
javadoc {
    options.noQualifiers = ['all']
    options.links = ['https://docs.oracle.com/javase/7/docs/api/']
    exclude "**/SWIGTYPE*"
    exclude "**/error_carrier*"
    exclude "**/batch_request*"
    exclude "**/batch_result*"
    exclude "**/qdbJNI*"
    exclude "**/qdb*"
    exclude "**/results_list*"
    exclude "**/retval*"
    exclude "**/run_batch_result*"
    exclude "**/ApiBufferPtr*"
    exclude "**/BatchOpsVec*"
    exclude "**/OperationHasValue*"
    exclude "**/RemoteNode*"
    exclude "**/StringVec*"
    exclude "**/TypeOperationMap*"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}
