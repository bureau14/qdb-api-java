apply plugin: 'java'
group = 'net.quasardb.qdb'
version = '2.0.0'
archivesBaseName = 'net.quasardb.qdb'

String arch = System.getProperty("os.arch").toLowerCase();
if (arch.endsWith("64")) arch = "x64"
else arch = "x86"

String os = System.getProperty("os.name").toLowerCase();
if (os.startsWith("windows")) os = "windows"
if (os.startsWith("mac")) os = "macosx"

repositories {
   flatDir {
       dirs '../qdb'
   }
}

dependencies {
   compile name: "net.quasardb.qdb.jni", version: "$version"
   runtime name: "net.quasardb.qdb.jni", version: "$version", classifier: "$os-$arch"
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

// http://docs.oracle.com/javase/1.5.0/docs/tooldocs/windows/javadoc.html#options
javadoc {
    options.noQualifiers = ['all']
    options.links = ['https://docs.oracle.com/javase/7/docs/api/']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
